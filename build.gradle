buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        gradlePluginPortal()
        maven { url = 'maven.neoforged.net' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
    }
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'groovy'
    id 'eclipse'
    id 'maven-publish'
    id 'fabric-loom' version "${loom_version}" apply false
    id 'net.minecraftforge.gradle' version '[6.0,6.2)' apply false
    id 'net.neoforged.moddev.legacyforge' version "${neoforge_version}" apply false
    id 'org.parchmentmc.librarian.forgegradle' version '1.+' apply false
    id 'org.spongepowered.mixin' version '0.7.+' apply false
}

group = mod_group_id
version = mod_version

// Set temp variable for artifact ID
def artifact_id = ""

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

def isForge = project.findProperty('forge') == 'true'
def isFabric = project.findProperty('fabric') == 'true'
def isNeoForge = project.findProperty('neoforge') == 'true'

if (isForge) {
    println "Building with Forge"

    apply plugin: 'net.minecraftforge.gradle'
    apply plugin: 'org.spongepowered.mixin'
    apply plugin: 'org.parchmentmc.librarian.forgegradle'

    // Exclude files marked as "fabric only"
    sourceSets.main.java.exclude '**/*_Fabric.java'
    artifact_id = "${mod_id}_forge"

    dependencies {
        minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
        implementation "software.bernie.geckolib:geckolib-forge-${minecraft_version}:${geckolib_version}"
        implementation "com.eliotlash.mclib:mclib:20"
        implementation 'org.slf4j:slf4j-api:2.0.17'
        implementation 'com.electronwill.night-config:core:3.8.3'
        implementation 'org.jspecify:jspecify:0.3.0'
    }

    minecraft {
        mappings channel: mapping_channel, version: "${parchment_version}-${minecraft_version}"
        runs {
            client {
                workingDirectory project.file('run')
                property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
                property 'forge.logging.console.level', 'debug'
            }
        }
    }
}
else if (isFabric) {
    println "Building with Fabric"

    apply plugin: 'fabric-loom'

    // Exclude files marked as "forge only"
    sourceSets.main.java.exclude '**/*_Forge.java'
    artifact_id = "${mod_id}_fabric"

    dependencies {
        minecraft "com.mojang:minecraft:${minecraft_version}"
        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${minecraft_version}:${parchment_version}@zip")
        }
        modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
        modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_api_version}"
        modImplementation("software.bernie.geckolib:geckolib-fabric-${minecraft_version}:${geckolib_version}")
        implementation("com.eliotlash.mclib:mclib:20")
        implementation 'org.slf4j:slf4j-api:2.0.17'
        implementation 'com.electronwill.night-config:core:3.8.3'
        implementation 'org.jspecify:jspecify:0.3.0'
    }

    loom {
        runs {
            client {
                property 'fabric.debug', 'true'
            }
        }
    }
}
else {
    println "Building with NeoForge"

    apply plugin: 'net.neoforged.moddev.legacyforge'

    // Exclude files marked as "fabric only"
    sourceSets.main.java.exclude '**/*_Fabric.java'
    artifact_id = "${mod_id}_neoforge"

    dependencies {
        // minecraft "net.neoforged:forge:${minecraft_version}-${neoforge_version}"
        implementation "software.bernie.geckolib:geckolib-forge-${minecraft_version}:${geckolib_version}"
        implementation "org.parchmentmc.data:parchment-1.20.1:2023.09.03@zip"
        implementation "com.eliotlash.mclib:mclib:20"
        implementation 'org.slf4j:slf4j-api:2.0.17'
        implementation 'com.electronwill.night-config:core:3.8.3'
        implementation 'org.jspecify:jspecify:0.3.0'
    }

    legacyForge {
        version = "${minecraft_version}-${forge_version}"
        // mappings {
        //     parchment {
        //         minecraftVersion = '1.20.1'
        //         mappingsVersion = '2023.09.03'
        //     }
        // }
    }
}

repositories {
    mavenCentral()
    maven { url 'libraries.minecraft.net' }
    maven { url 'https://maven.minecraftforge.net' }
    maven { url 'https://repo.spongepowered.org/maven' }
    maven { url 'https://maven.neoforged.net/' }
    maven { url 'https://maven.fabricmc.net/' }
    maven { url 'https://maven.parchmentmc.org/' }
    maven {
        name = 'GeckoLib'
        url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
        content {
            includeGroupByRegex("software\\.bernie.*")
            includeGroup("com.eliotlash.mclib")
        }
    }
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            artifactId "${artifact_id}"
            artifact jar
        }
    }
}